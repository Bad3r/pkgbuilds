# Maintainer: WhiredPlanck
# Maintainer: Vyacheslav Konovalov <f4f7l1hvr@relay.firefox.com>

pkgname=slack-electron
pkgver=4.14.0
pkgrel=1
pkgdesc="Slack Desktop (Beta) for Linux, modified to use the system 'electron' package instead of its own"
arch=('x86_64')
url="https://slack.com/downloads"
license=('custom: Slack')
depends=('electron')
optdepends=('libappindicator-gtk3: for notification indicator in the status bar on Gnome')
provides=('slack' 'slack-desktop')
conflicts=('slack-desktop')
source=("https://downloads.slack-edge.com/linux_releases/slack-desktop-$pkgver-amd64.deb"
        'slack.sh')
sha256sums=('cfa500b6c5109724d5fc123477d5e4c9740e332a56397a1413462da9358ac977'
            '4db11d083626a561129ca40e9cce7cc613c42223f138bb0136c62064c57471fd')

prepare() {
  bsdtar -xf data.tar.xz

  # Enable slack silent mode and fix icon
  sed -ri 's|^(Exec=.+/slack)(.+)|\1 -s\2|' $srcdir/usr/share/applications/${pkgname%-electron}.desktop
  sed -ri 's/^Icon=.+slack\.png/Icon=slack/' $srcdir/usr/share/applications/${pkgname%-electron}.desktop
}

package() {
  install -Dm755 slack.sh "$pkgdir/usr/bin/${pkgname%-electron}"

  install -dm755 "$pkgdir/usr/lib/${pkgname%-electron}/"
  cp -dr --no-preserve=ownership usr/lib/slack/resources/* "$pkgdir/usr/lib/${pkgname%-electron}/"

  install -Dm644 usr/share/applications/slack.desktop -t "$pkgdir/usr/share/applications"
  install -Dm644 usr/share/pixmaps/slack.png -t "$pkgdir/usr/share/pixmaps"
  install -Dm644 "$srcdir/usr/lib/slack/LICENSE" "$pkgdir/usr/share/licenses/${pkgname%-electron}/LICENSE"
}
